buildscript {
    
    repositories {
        jcenter()
    }
    
    dependencies {
        classpath 'com.bmuschko:gradle-cargo-plugin:2.0.2'  
    }
}

apply {
    plugin 'java'
    plugin 'maven'
    plugin 'jetty'    
    plugin 'war'
    plugin 'com.bmuschko.cargo'
}
 
group = projectGroup
archivesBaseName = projectWarBaseName
version = projectVersion

sourceCompatibility = projectSourceCompatibility
targetCompatibility = projectTargetCompatibility
 
[compileJava, compileTestJava]*.options*.encoding = projectEncoding
 
 
repositories {
    mavenCentral()
}
 

dependencies {
    cargo group: 'org.jboss.as', name: 'jboss-as-controller-client', version: '7.1.1.Final'
    def cargoVersion = '1.4.5'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion",
          "org.codehaus.cargo:cargo-ant:$cargoVersion"
    
    providedCompile "javax:javaee-api:$javaeeVersion"
                    
    compile "javax.json:javax.json-api:1.0",
            "org.glassfish:javax.json:1.0.4"
                    
    runtime "javax.json:javax.json-api:1.0",
            "org.glassfish:javax.json:1.0.4"
}

javadoc {
    options.encoding = projectEncoding
}
 
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'

    from javadoc.destinationDir
}
 
task sourcesJar(type: Jar) {
    classifier = 'sources'
 
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

cargo {
    containerId = 'jboss7x'
    port = 9990

    deployable {
        file = file('build/libs/whiteboard-1.0.war')
    }

    remote {
        hostname = '127.0.0.1'
        username = 'xinto'
        password = 'X1nt016.'
    }
}